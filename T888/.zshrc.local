source /usr/share/doc/pkgfile/command-not-found.zsh
# load libraries
function () {
  typeset lib
  for lib (~/lib/*.zsh) source ${lib}
}

bindkey -v

# It is standard to use '$' for a non-root user. Not '%'. C'mon grml...
grml_prompt_token_default[percent]='$ '

export BROWSER=chromium
export PATH=$PATH:~/bin:~/scrap
export TERMINAL=jp-terminal
export VISUAL=/usr/bin/vim
export KEYTIMEOUT=1

alias -s pdf=zathura
alias -s zsh=vim
alias -s txt=vim

#TODO: don't log bad sessions
function ssh() {
  typeset arg=1
  typeset logtmp
  typeset logfile
  typeset -r logdir="${HOME}/.ssh/logs"
  typeset -a args
  args=("$@")
  readonly -a args

  while [[ -z "${logfile}" ]] && [[ "${arg}" -le "${#args}" ]]; do
    case ${args[arg]} in
      (-[1246AaCfGgKkMNnqsTtVvXxYy]*) (( arg++ )) ;;
      (-*) (( arg+=2 )) ;;
      (*) readonly logfile="${logdir}/$(date +%Y%b%d-%T) ${args[arg]}.log.asc" ;;
    esac
  done

  if [[ -n "${logfile}" ]]; then
    readonly logtmp="/tmp/${logfile:t:r}"
    =ssh $args | tee >(col -b > "${logtmp}")
    [[ -s "${logtmp}" ]] && gpg --output "${logfile}" -ear $USER "${logtmp}"
    rm "${logtmp}"
  else
    =ssh $args
  fi
}

source /usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
## END OF FILE #################################################################
# vim:filetype=zsh foldmethod=marker autoindent expandtab shiftwidth=2
#
