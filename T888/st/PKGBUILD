# Contributor: Patrick Jackson <PatrickSJackson gmail com>
# Maintainer: Christoph Vigano <mail@cvigano.de>

pkgname=st
pkgver=0.6
pkgrel=2
pkgdesc='A simple virtual terminal emulator for X.'
arch=('i686' 'x86_64')
license=('MIT')
depends=('libxext' 'libxft' 'ttf-anonymous-pro')
makedepends=('ncurses')
url="http://st.suckless.org"
source=("http://dl.suckless.org/st/$pkgname-$pkgver.tar.gz"
        "http://st.suckless.org/patches/st-$pkgver-hidecursor.diff"
        'config.h')
sha256sums=('85310c08c3814e099b5e45888f8ac7548640e69e9973e934d4fba38b03737a2b'
            'f59c797e49a7f6a9942ca95b214d996207cd77eb099c679cf16dca10311b4cfd'
            '236c652036d24e94d7036602b4c330d41848a2d2b2aebb7ed367203af81758be')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  # skip terminfo which conflicts with ncurses
  sed -i '/\@tic /d' Makefile
  cp $srcdir/config.h config.h
}

build() {
  cd $srcdir/$pkgname-$pkgver
  patch -p1 < $srcdir/st-$pkgver-hidecursor.diff
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make PREFIX=/usr DESTDIR="$pkgdir" TERMINFO="$pkgdir/usr/share/terminfo" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
}
